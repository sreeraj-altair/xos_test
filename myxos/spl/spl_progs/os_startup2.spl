breakpoint;
load (7,1);		//Load Exception Handler
load (8,2);		//Load Exception Handler
load (23,17);		//load Interrupt 7
load (24,18);		//load Interrupt 7

[READY_LIST + 0] = 0;		// Sets the entry for PID as 0

PTBR = 1024;
PTLR = 4;

[PTBR + 0] = 21; 		// 1st Word of Page Table Entry 1 - Page Number
[PTBR + 1] = "00"; 		// 2nd Word of Page Table Entry 1 - Auxiliary Information
[PTBR + 2] = 22; 		// 1st Word of Page Table Entry 2 - Page Number
[PTBR + 3] = "00";		// 2nd Word of Page Table Entry 2 - Auxiliary Information
[PTBR + 4] = 23; 		// 1st Word of Page Table Entry 3 - Page Number
[PTBR + 5] = "00";		// 2nd Word of Page Table Entry 3 - Auxiliary Information
[PTBR + 6] = 25; 		// 1st Word of Page Table Entry 4 - Page Number of stack 
[PTBR + 7] = "01"; 		// 2nd Word of Page Table Entry 4 - Auxiliary Information

[READY_LIST + 1] = 2;	 	// Sets the entry for STATE as 2 (Running)

SP = 3 * 512;

[25 * 512] = 0;

load (11, 5);		//Load Interrupt 1
load (12, 6);		//Load Interrupt 1
load (13, 7);		//Load Interrupt 2
load (14, 8);		//Load Interrupt 2
load (15, 9);		//Load Interrupt 3
load (16, 10);		//Load Interrupt 3
load (17, 11);		//Load Interrupt 4
load (18, 12);		//Load Interrupt 4
load (19, 13);		//Load Interrupt 5
load (20, 14);		//Load Interrupt 5
load (21, 15);		//Load Interrupt 6
load (22, 16);		//Load Interrupt 6
load (23, 17);		//Load Interrupt 7
load (24, 18);		//Load Interrupt 7


load (9, 3);		//Load Timer Interrupt
load (10, 4);		//Load Timer Interrupt


load (5,19);		//FAT

load (6,20);		//Disk Free List

alias counter S0;			

//initialize system wide open file table
counter = 0;
while (counter <  128) do			// 64 entries, each of size 2 words
	[ FILE_TABLE + counter + 0] = -1;	// Set FAT Index to -1
	[ FILE_TABLE + counter + 1] =  0;	// Set File Open Count to 0
	counter = counter + 2;			// Move to next entry
endwhile;

//Initialize state of all pcbs except inits to 0
counter = 1;
while (counter < 32) do
	[READY_LIST + counter*32 + 1] = 0;
	counter = counter + 1;
endwhile;

//Initialize open file table of init
counter = 15;
while (counter < 31) do
	[READY_LIST + counter] = -1;
	counter = counter + 1;
endwhile;

//frees swap area
counter = 448;
while (counter < 512) do
	[DISK_LIST + counter] = 0;
	counter = counter + 1;
endwhile;
store(6,20);

//initialize memory free list
counter = 0;
while (counter < 64) do
	if (counter <= 25) then
		[MEM_LIST + counter] = 1;
	else
		[MEM_LIST + counter] = 0;
	endif;
	counter = counter + 1;
endwhile;
breakpoint;
ireturn;
